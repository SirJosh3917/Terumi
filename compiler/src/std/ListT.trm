use std
// This class is an implementation of a list. Since Terumi does not yet support generics at the time of writing, this is to be copied and
// pasted wherever a List of T is needed.

// wrapper class to let this code compile
// ctrl + f "Object" -> "YourType"
class Object
{
	string data

	ctor(string param_data) {
		data = param_data
	}
	
	ctor() {
		data = ""
	}
	
	string to_string() return data
	bool equals(Object other) return other.data == data
}

ListObject ListObject() return new ListObject()
ListObject ListObject(Object v1) return _chain_add(ListObject(), v1)
ListObject ListObject(Object v1, Object v2) return _chain_add(ListObject(v1), v2)
ListObject ListObject(Object v1, Object v2, Object v3) return _chain_add(ListObject(v1, v2), v3)
ListObject ListObject(Object v1, Object v2, Object v3, Object v4) return _chain_add(ListObject(v1, v2, v3), v4)
ListObject ListObject(Object v1, Object v2, Object v3, Object v4, Object v5) return _chain_add(ListObject(v1, v2, v3, v4), v5)
ListObject ListObject(Object v1, Object v2, Object v3, Object v4, Object v5, Object v6) return _chain_add(ListObject(v1, v2, v3, v4, v5), v6)
ListObject ListObject(Object v1, Object v2, Object v3, Object v4, Object v5, Object v6, Object v7) return _chain_add(ListObject(v1, v2, v3, v4, v5, v6), v7)
ListObject ListObject(Object v1, Object v2, Object v3, Object v4, Object v5, Object v6, Object v7, Object v8) return _chain_add(ListObject(v1, v2, v3, v4, v5, v6, v7), v8)

ListObject _chain_add(ListObject instance, Object value)
{
	instance.add(value)
	return instance
}

class ObjectPredicate
{
	bool matches(Object instance) {
		return false
	}
}

class ListObject
{
	string _to_string_object(Object other) return other.to_string()
	bool _equals_object(Object left, Object right) return left.equals(right)
	
	_ListObjectNode _root
	_ListObjectNode _tail
	
	ctor() {
		_root = new _ListObjectNode()
		_tail = _root
	}
	
	number length() {
		number count = 0
		
		ListObjectEnumerator enumerator = enumerate()
		while enumerator.has_next() {
			count++
		}
		
		return count
	}
	
	bool has_any() {
		return _root.has_value
	}

	add(Object value) {
		_tail.value(value)
		
		_ListObjectNode new_tail = new _ListObjectNode()
		new_tail.prev(_tail)
		_tail.next(new_tail)
		_tail = new_tail
	}
	
	removeAt(number index) {
		number i = 0
		
		if index == 0 {
			if !has_any() {
				@panic("ListObject.removeAt(number): attempted to remove element at position 0 when none exists")
			}
			
			_root = _root.safe_next()
			return
		}
		
		ListObjectEnumerator enumerator = enumerate()
		while @operator_and(i <= index, enumerator.has_next()) {
			if i == index {
				_ListObjectNode node = enumerator._current
				
				if @operator_and(node.has_prev, node.has_next) {
					// stitch previous node to refer to next node,
					// and next node to refer to previous node
					node.safe_prev().next(node.safe_next())
					node.safe_next().prev(node.safe_prev())
				}
				else if node.has_prev {
					// if it only has a previous, clear the previous node's next
					node.safe_prev().next()
				}
				else if node.has_next {
					// if it only has a next, clear the next node's previous
					node.safe_next().prev()
				}
				else {
					@panic("ListObject.removeAt(number): unsure how to remove node from chain. has neither prev nor next.")
					return
				}
				
				return
			}
			i++
		}
		i--
		
		@panic("ListObject.removeAt(number): unable to remove element at index {to_string(index)} (enumerated up until {to_string(i)})")
	}
	
	clear() {
		ctor()
	}
	
	string to_string() {
		string total = "["
		
		bool has_enumerated = false
		ListObjectEnumerator enumerator = enumerate()
		while enumerator.has_next() {
			if has_enumerated {
				total = "{total}, {_to_string_object(enumerator.current)}"
			}
			else {
				total = "{total}{_to_string_object(enumerator.current)}"
				has_enumerated = true
			}
		}
		
		return "{total}]"
	}
	
	bool equals(ListObject other) {
		ListObjectEnumerator enumerateThis = enumerate()
		ListObjectEnumerator enumerateThat = other.enumerate()
		
		do {
			bool thisNext = enumerateThis.has_next()
			bool thatNext = enumerateThat.has_next()
			
			if thisNext != thatNext return false
			
			// if there are no more items, quit
			// we've verified that the two are the same so we don't need to check both
			if !thisNext return true
			
			// they were both true so we can guarantee that there's an item
			if !_equals_object(enumerateThis.current, enumerateThat.current) return false
		} while(true)
	}
	
	bool contains(ObjectPredicate predicate) {
		return index_of(predicate) != -1
	}
	
	number index_of(ObjectPredicate predicate) {
		number i = 0
		
		ListObjectEnumerator enumerator = enumerate()
		while enumerator.has_next() {
			if predicate.matches(enumerator.current) return i
			i++
		}
		
		return (-1)
	}
	
	Object at(number index) {
		number i = 0
		
		ListObjectEnumerator enumerator = enumerate()
		while @operator_and(i <= index, enumerator.has_next()) {
			if i == index return enumerator.current
			i++
		}
		i--
		
		@panic("Object ListObject.at(number): unable to find element at index {to_string(index)} (enumerated up until {to_string(i)})")
	}
	
	insert(number index, Object value) {
		number i = 0
		
		if index == 0 {
			_ListObjectNode new_root = new _ListObjectNode()
			new_root.value(value)
			new_root.next(_root)
			_root = new_root
			return
		}
		
		ListObjectEnumerator enumerator = enumerate()
		while @operator_and(i <= index, enumerator.has_next()) {
			if i == index {
				_ListObjectNode node = enumerator._current
				
				// set this node's previous entry to point to the next one
				// we handled the case where i is 0, so we know we must have a previous node
				_ListObjectNode inject = new _ListObjectNode()
				inject.value(value)
				inject.next(node)
				inject.prev(node.safe_prev())
				
				node.safe_prev().next(inject)
				
				return
			}
			i++
		}
		
		if i == index {
			// we're inserting at the end - aka, adding
			add(value)
			return
		}
		
		i--
		
		@panic("ListObject.insert(number, Object): unable to insert element at index {to_string(index)} (enumerated up until {to_string(i)})")
	}
	
	ListObjectEnumerator enumerate() {
		return new ListObjectEnumerator(_root)
	}
	
	_debug_print_structure() {
		@println("_root: ")
		_root._debug_print_structure("  ")
	}
}

// ListObjectEnumerator enumerator = list.enumerate()
// while enumerator.has_next() {
//     Object next = enumerator.current
// }

class ListObjectEnumerator
{
	Object current
	
	bool _has_enumerated
	_ListObjectNode _current

	ctor(_ListObjectNode node_current) {
		_has_enumerated = false
		_current = node_current
	}

	bool has_next() {
		if !_has_enumerated {
			if _current.has_value {
				current = _current.safe_value()
				
				_has_enumerated = true
				return true
			}
			
			return false
		}
		
		if _current.has_next {
			_current = _current.safe_next()
			
			if _current.has_value {
				current = _current.safe_value()
				return true
			}
		}
		
		return false
	}
}

class _ListObjectNode
{
	bool has_value
	Object value
	
	bool has_next
	_ListObjectNode next
	
	bool has_prev
	_ListObjectNode prev
	
	ctor() {
		has_value = false
		has_next = false
		has_prev = false
	}
	
	Object safe_value() {
		if !has_value {
			@panic("Object _ListObjectNode.safe_value(): attempted to take value when no value present")
		}
		
		return value
	}
	
	_ListObjectNode safe_next() {
		if !has_next {
			@panic("_ListObjectNode _ListObjectNode.safe_next(): attempted to take next when no next present")
		}
		
		return next
	}
	
	_ListObjectNode safe_prev() {
		if !has_prev {
			@panic("_ListObjectNode _ListObjectNode.safe_prev(): attempted to take prev when no prev present")
		}
		
		return prev
	}
	
	value() {
		has_value = false
	}
	
	value(Object new_value) {
		has_value = true
		value = new_value
	}
	
	next() {
		has_next = false
	}
	
	next(_ListObjectNode new_next) {
		has_next = true
		next = new_next
	}
	
	prev() {
		has_prev = false
	}
	
	prev(_ListObjectNode new_prev) {
		has_prev = true
		prev = new_prev
	}
	
	_debug_print_structure(string tab) {
		@println("{tab}- has_value: {to_string(has_value)}")
		if has_value {
			@println("{tab}  {new ListObject()._to_string_object(value)}")
		}
		
		@println("{tab}- has_prev: {to_string(has_prev)}")
		
		@println("{tab}- has_next: {to_string(has_next)}")
		if has_next {
			next._debug_print_structure("{tab}  ")
		}
	}
}